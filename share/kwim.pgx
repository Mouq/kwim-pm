document: block-top*

block-top:
  | block-blank
  | block-comment
  | line-comment
  | block-head
  | block-pref
  | block-list
  | block-title
  | block-verse
  | block-para

block-blank: /
  line-blank
/

block-comment: /
  HASH HASH HASH EOL
  (
    (: ANY*? EOL)*?
  )
  HASH HASH HASH EOL
  line-blank?
/

line-comment: /
  HASH SPACE? ( ANY*? ) EOL
  line-blank?
/

block-head: /
  ( EQUAL{1,4} ) SPACE+ (:
    ( ANY+? ) SPACE+ EQUAL+ EOL
  | ( ANY+ NL (: [^ WS ] ANY* EOL )* [^ WS ] ANY*? ) SPACE+ EQUAL+ EOL
  | ( ANY+ NL (: [^ WS ] ANY* EOL )*) (= [ start-block ] | EOL | EOS)
  ) line-blank?
/

block-pref: /
  (
    (:
      line-blank*
      SPACE SPACE ANY* EOL
    )+
  )
  line-blank?
/

block-list: /(
  line-list-item
  (: line-list-item | line-blank* line-indented )*
  line-blank?
)/

line-list-item: /
  STAR SPACE ANY* EOL
/

block-list-item: (
  | block-blank
  | block-comment
  | line-comment
  | block-head
  | block-pref
  | block-list
  | block-title
  | block-verse
  | block-para
)*

line-indented: /
  SPACE SPACE ANY* EOL
/

block-title: /
  ( text-line )
  EQUAL{3,} EOL
  line-blank?
/

block-verse: /
  DOT NL
  ( text-line+ )
  line-blank?
/

# block-para:
#   paragraph-flow
# #   paragraph-hard

# list-unordered: list-entry+
# block-code: â€¦

block-para: /
  ( text-line+ )
  line-blank?
/

text-markup: phrase-markup+

phrase-markup:
  | char-escape
  | phrase-text
  | phrase-bold
  | phrase-emph
  | phrase-code
#   | phrase-link
#   | char-next

char-escape: / BACK ( ANY ) /

phrase-text: /
  ( [^ char-phrase-start]+ )
/

phrase-code: /
  GRAVE
  ( [^ GRAVE]*? )
  GRAVE
/

phrase-bold:
  char-bold
  ( !char-bold phrase-markup )+
  char-bold

phrase-emph:
  char-emph
  ( !char-emph phrase-markup )+
  char-emph

text-line: / (: [^ start-block NL ] ANY* NL ) /

line-blank: / (: SPACE* EOL ) /

start-pref: / SPACE /
start-list: / STAR /
start-head: / EQUAL /
start-comment: / EQUAL /

start-block: /
  start-pref
  start-list
  start-head
  start-comment
/

char-bold: / STAR /
char-emph: / SLASH /
char-code: / GRAVE /

char-phrase-start: /
  char-bold
  char-emph
  char-code
/

# vim: set lisp sw=2:
